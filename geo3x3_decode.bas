1 'GEO3x3 decode
10 CLV:N=20
12 S="E9139659937288"

'main
14 F=ASC(S)=87
16 LET[0],1,8,0
18 FORJ=1TOLEN(S)-1
20  M=ASC(S+J)-49:?M+1
22  X=3:A=0:GSB@XDIV
24  A=0:B=60:GSB@A2B:X=M%3:A=60:GSB@XMUL:B=40:C=40:GSB@ADD
26  A=0:B=60:GSB@A2B:X=M/3:A=60:GSB@XMUL:B=20:C=20:GSB@ADD
28 NEXT
30 X=2:A=0:GSB@XDIV
40 A=0:B=20:C=20:GSB@ADD
50 A=0:B=40:C=40:GSB@ADD
60 LET[80],0,9,0:A=20:B=80:C=20:GSB@SUB
70 IF F LET[80],1,8,0:A=40:B=80:C=40:GSB@SUB
80 S=20:GSB@SHOW:S=40:GSB@SHOW
90 END

'util
100 @ADD:FORI=0TON-1:[C+I]=[A+I]+[B+I]:NEXT:A=C
110 @NORM:FORI=N-1TO1STEP-1:[A+I-1]=[A+I-1]+[A+I]/10:[A+I]=[A+I]%10:NEXT:RTN

150 @SUB:FORI=N-1TO0STEP-1:IF[A+I]<[B+I]&&I>0[A+I-1]=[A+I-1]-1:[A+I]=[A+I]+10
170 [C+I]=[A+I]-[B+I]:NEXT:RTN

200 @SHOW:?[S];[S+1];[S+2];".";:FORI=3TON-1:?[S+I];:NEXT:?:RTN

400 @XMUL:FORI=N-1TO0STEP-1:[A+I]=[A+I]*X:NEXT:GOTO@NORM

450 @XDIV:FORI=0TON-1:D=[A+I]%X:[A+I]=[A+I]/X:IFI<N-1[A+I+1]=[A+I+1]+D*10
460 NEXT:RTN

470 @DIVX:X=0
480 GSB@SUB:IF[C]<0:RTNELSEX=X+1:A=C:CONT

500 @A2B:FORI=0TON-1:[B+I]=[A+I]:NEXT:RTN
