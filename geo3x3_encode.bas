1 'GEO3x3 encode
10 CLV:N=20:L=14
20 LET[20],0,3,5,6,5,8,5,8
25 LET[40],1,3,9,7,4,5,4,3,3

'main
30 LET[0],1,8,0:[21]=[21]+9:A=20:GSB@NORM
35 IF[40]>=0?"E";ELSE?"W";:LET[60],1,8,0:[40]=-[40]:A=60:B=40:C=40:GSB@SUB
40 FORJ=1TOL-1
50  A=0:X=3:GSB@XDIV
60  A=20:B=60:GSB@A2B:A=60:B=0:C=80:GSB@DIVX:Y=X
70  A=40:B=60:GSB@A2B:A=60:B=0:GSB@DIVX
80  ?X+Y*3+1;
90  A=0:B=60:GSB@A2B:A=60:GSB@XMUL:A=40:B=60:C=40:GSB@SUB
95  A=0:B=60:GSB@A2B:A=60:X=Y:GSB@XMUL:A=20:B=60:C=20:GSB@SUB
99 NEXT:?:END

'util
100 @ADD:FORI=0TON-1:[C+I]=[A+I]+[B+I]:NEXT:A=C
110 @NORM:FORI=N-1TO1STEP-1:[A+I-1]=[A+I-1]+[A+I]/10:[A+I]=[A+I]%10:NEXT:RTN

150 @SUB:FORI=N-1TO0STEP-1:IF[A+I]<[B+I]&&I>0[A+I-1]=[A+I-1]-1:[A+I]=[A+I]+10
170 [C+I]=[A+I]-[B+I]:NEXT:RTN

200 @SHOW:?[S];[S+1];[S+2];".";:FORI=3TON-1:?[S+I];:NEXT:?:RTN

400 @XMUL:FORI=N-1TO0STEP-1:[A+I]=[A+I]*X:NEXT:GOTO@NORM

450 @XDIV:FORI=0TON-1:D=[A+I]%X:[A+I]=[A+I]/X:IFI<N-1[A+I+1]=[A+I+1]+D*10
460 NEXT:RTN

470 @DIVX:X=0
480 GSB@SUB:IF[C]<0:RTNELSEX=X+1:A=C:CONT

500 @A2B:FORI=0TON-1:[B+I]=[A+I]:NEXT:RTN
